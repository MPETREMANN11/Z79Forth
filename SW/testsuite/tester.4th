\ Ce 1995 JOHNS HOPKINS UNIVERSITY / APPLIED PHYSICS
\ LABORATORY MAY BE DISTRIBUTED FREELY AS LONG AS THIS
\ COPYRIGHT NOTICE REMAINS. VERSION 1.2

\ Adapted for Z79Forth from https://github.com/gerryjackson/
\ forth2012-test-suite/blob/master/src/core.fr

\ SET THE FOLLOWING FLAG TO TRUE FOR MORE VERBOSE OUTPUT;
\ THIS MAY ALLOW YOU TO TELL WHICH TEST CAUSED YOUR SYSTEM
\ TO HANG.
0 CONSTANT FALSE         1 CONSTANT TRUE

VARIABLE VERBOSE   TRUE VERBOSE !

VARIABLE #ERRORS 0 #ERRORS !

HEX

: EMPTY-STACK   ( ... -- ) BEGIN DEPTH WHILE DROP REPEAT ;
: RSHIFT NEGATE SHIFT ;
: LSHIFT SHIFT ;

: ERROR ( C-ADDR U -- )
  CR TYPE CR \ SOURCE DROP >IN @ 63 COM AND + 64 TYPE
  EMPTY-STACK          \ THROW AWAY EVERY THING ELSE
  #ERRORS @ 1 + #ERRORS !
  ABORT  \ *** Uncomment this line to ABORT on error
;

VARIABLE ACTUAL-DEPTH   \ STACK RECORD
CREATE ACTUAL-RESULTS 20 CELLS ALLOT

: T{ ( -- ) ;           \ SYNTACTIC SUGAR.

: -> ( ... -- )         \ RECORD DEPTH AND CONTENT OF STACK.
  DEPTH DUP ACTUAL-DEPTH ! \ RECORD DEPTH
  ?DUP IF              \ IF THERE IS SOMETHING ON STACK
    0 DO ACTUAL-RESULTS I CELLS + ! LOOP \ SAVE THEM
  THEN ;

: }T ( ... -- ) \ COMPARE STACK (EXPECTED) CONTENTS WITH SAVED
  \ (ACTUAL) CONTENTS.
  DEPTH ACTUAL-DEPTH @ = IF      \ IF DEPTHS MATCH
    DEPTH ?DUP IF 0   \ IF THERE IS SOMETHING ON THE STACK
      DO          \ FOR EACH STACK ITEM
        ACTUAL-RESULTS I CELLS + @ \ CMP ACTUAL VS EXPECTED
        = 0= IF S" INCORRECT RESULT: " ERROR LEAVE THEN
      LOOP
    THEN
  ELSE                 \ DEPTH MISMATCH
    S" WRONG NUMBER OF RESULTS: " ERROR
  THEN ;

: TESTING ( -- )        \ TALKING COMMENT.
  [CHAR] . WORD COUNT
  VERBOSE @ IF TYPE CR
  ELSE 2DROP [CHAR] * EMIT THEN ;
                                           DECIMAL 405 432 THRU
